
/**
 * Common Gradle script configuration for simple projects.  Made to be applied
 * _after_ all standard plugins.
 *
 * @author Emanuel Rabina
 */

/* 
 * Introduce 'optional' and 'provided' configuration modifiers.
 */
project.ext.optionalDependencies = []
project.ext.optional = {
	optionalDependencies << it
}
project.ext.providedDependencies = []
project.ext.provided = {
	providedDependencies << it
}

/* 
 * Include Maven central and snapshot repositories by default.
 */
repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
}

/* 
 * Adjust plugins, source and build directories, and any standard Gradle tasks
 * for the way I use them.
 */
def sourceDir  = ['Source']
def testDir    = ['Tests']
def contentDir = 'Content'

if (plugins.hasPlugin('java')) {
	sourceSets {
		main {
			java.srcDirs        = sourceDir
			resources.srcDirs   = sourceDir
			output.resourcesDir = output.classesDir
	
		}
		test {
			java.srcDirs        = testDir
			resources.srcDirs   = testDir
			output.resourcesDir = output.classesDir
		}
	}
	jar {
		dependsOn test
		duplicatesStrategy 'exclude'
	}
}

if (plugins.hasPlugin('groovy')) {
	sourceSets {
		main.groovy.srcDirs = sourceDir
		test.groovy.srcDirs = sourceDir
	}
}

if (plugins.hasPlugin('war')) {
	project.webAppDirName = 'Content'
}

if (plugins.hasPlugin('eclipse')) {
	eclipse.classpath.defaultOutputDir = file(plugins.hasPlugin('war') ?
		"${webAppDir}/WEB-INF/classes" :
		"${projectDir}/Classes"
	)
	eclipse.jdt {
		sourceCompatibility = project.sourceCompatibility
		targetCompatibility = project.sourceCompatibility
	}
}
