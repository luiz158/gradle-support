
/**
 * Common Gradle script configuration for simple projects.
 *
 * @author Emanuel Rabina
 */

/* 
 * Introduce 'optional' and 'provided' configuration modifiers.
 */
project.ext.optionalDependencies = []
project.ext.optional = {
	optionalDependencies << it
}
project.ext.providedDependencies = []
project.ext.provided = {
	providedDependencies << it
}

/* 
 * Include Maven central and snapshot repositories by default.
 */
repositories {
	mavenCentral()
	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots/'
	}
}

/* 
 * Make 'Source' the main source directory.
 */
def sourceDir = ['Source']
def testDir   = ['Tests']

sourceSets {
	main {
		if (java) {
			java.srcDirs = sourceDir
		}
		if (groovy) {
			groovy.srcDirs = sourceDir
		}
		resources.srcDirs = sourceDir
		output.resourcesDir = output.classesDir
	}
	test {
		if (java) {
			java.srcDirs = testDir
		}
		if (groovy) {
			groovy.srcDirs = testDir
		}
		resources.srcDirs = testDir
		output.resourcesDir = output.classesDir
	}
}

jar {
	dependsOn test
	duplicatesStrategy 'exclude'
}
