
/**
 * Common Gradle script configuration for projects that need to publish to
 * Maven repositories.
 * 
 * @author Emanuel Rabina
 */

apply plugin: 'signing'

project.ext.artifact     = ''
project.ext.year         = ''
project.ext.contributors = []

/**
 * Introduce 'optional' and 'provided' configuration modifiers.
 */
project.ext.optionalDependencies = []
project.ext.optional = {
	optionalDependencies << it
}
project.ext.providedDependencies = []
project.ext.provided = {
	providedDependencies << it
}

/**
 * Generate the pom.xml file, modifying the dependencies to support the optional
 * and provided configurations.
 */
def generatePom() {
	def pom = pom {
		project {
			artifactId    project.artifact
			packaging     'jar'
			name          project.name
			description   project.description
			url           "http://www.ultraq.net.nz/programming/${project.artifact}/"
			inceptionYear project.year
			licenses {
				license {
					name         'The Apache Software License, Version 2.0'
					url          'http://www.apache.org/licenses/LICENSE-2.0.txt'
					distribution 'repo'
				}
			}
			scm {
				url                 "scm:git:git@github.com:ultraq/${project.artifact}.git"
				connection          "scm:git:git@github.com:ultraq/${project.artifact}.git"
				developerConnection "scm:git:git@github.com:ultraq/${project.artifact}.git"
			}
			developers {
				developer {
					id    'emanuelrabina'
					name  'Emanuel Rabina'
					url   'http://www.ultraq.net.nz/'
					roles {
						role 'author'
						role 'developer'
					}
					timezone '+12'
				}
			}
			if (!project.contributors.isEmpty()) {
				contributors {
					project.contributors.each { c ->
						contributor {
							name  c.name
							email c.email
							roles {
								c.roles.each { r ->
									role r.role
								}
							}
							properties {
								contribution c.contribution
							}
						}
					}
				}
			}
		}
	}.whenConfigured { pom ->
		optionalDependencies.each { dep ->
			pom.dependencies.find { it.artifactId == dep.name }.optional = true
		}
		providedDependencies.each { dep ->
			pom.dependencies.find { it.artifactId == dep.name }.scope = 'provided'
		}
	}
}

/**
 * Write the pom files included in the JAR for distribution to a Maven
 * repository.
 */
task writePoms {
	ext.pomXml        = file("${project.mavenPomDir}/pom.xml")
	ext.pomProperties = file("${project.mavenPomDir}/pom.properties")
	inputs.file(project.buildFile)
	outputs.files(pomXml, pomProperties)
	doLast {
		generatePom().writeTo(pomXml)
		pomProperties.withWriter { out ->
			out.println '#Generated using Gradle'
			out.println '#' + new Date().format('EEE MMM dd HH:mm:ss zzz yyyy')
			out.println "version=${project.version}"
			out.println "groupId=${project.group}"
			out.println "artifactId=${project.artifact}"
		}
	}
}

/**
 * Reconfigure the JAR task to include the pom files normally generated by
 * Maven.
 */
jar {
	dependsOn writePoms
	doFirst {
		metaInf {
			from("${project.mavenPomDir}")
			into("maven/${project.group}/${project.artifact}")
			include('pom.*')
		}
	}
}

/**
 * Create the source file archive (JAR).
 */
task sourcesJar(type: Jar, dependsOn: classes) {
	doFirst {
		baseName = "${project.artifact}"
	}
	classifier = 'sources'
	sourceSets.main.java.srcDirs.each { dir ->
		from dir.absolutePath
	}
}

/**
 * Create the javadoc archive.
 */
task javadocJar(type: Jar, dependsOn: javadoc) {
	doFirst {
		baseName = "${project.artifact}"
	}
	classifier = 'javadoc'
	from javadoc.destinationDir
}

/**
 * Include source and javadoc JARs as part of the release.
 */
artifacts {
	archives sourcesJar, javadocJar
}

/**
 * Sign all JARs for Maven central.
 */
signing {
	sign configurations.archives
}

/**
 * Used for testing, upload everything to the local repository.
 */
install {
	doFirst {
		repositories.mavenDeployer.pom = generatePom()
	}
	repositories.mavenDeployer {
		configuration = configurations.archives
		repository(url: 'file:///' + System.properties['user.home'] + '/.m2/repository/')
		beforeDeployment { MavenDeployment deployment ->
			signing.signPom(deployment)
		}
	}
}

/**
 * Upload everything to Maven central.
 */
uploadArchives {
	ext.sonatypeUsername = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ''
	ext.sonatypePassword = project.hasProperty('sonatypePassword') ? sonatypePassword : ''

	doFirst {
		repositories.mavenDeployer.pom = generatePom()
	}
	repositories.mavenDeployer {
		configuration = configurations.archives
		repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
			authentication(userName: sonatypeUsername, password: sonatypePassword)
		}
		snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
			authentication(userName: sonatypeUsername, password: sonatypePassword)
		}
		beforeDeployment { MavenDeployment deployment ->
			signing.signPom(deployment)
		}
	}
}
